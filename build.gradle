// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.application = [id: 'com.addhen.checkin']
  ext.versions = [kotlin                    : '1.2.30',
                  gradlePlugin              : '3.1.0-rc01',
                  minSdkVersion             : 23,
                  targetSdkVersion          : 27,
                  compileSdkVersion         : 27,
                  supportLibrary            : '27.1.0',
                  constraintLayout          : '1.0.2',
                  leakcanary                : '1.5.4',
                  sourceCompatibilityVersion: JavaVersion.VERSION_1_8,
                  targetCompatibilityVersion: JavaVersion.VERSION_1_8,
                  dagger                    : '2.14.1',
                  archComponents            : '1.1.0',
                  timber                    : '4.6.1',
                  junit                     : '4.12',
                  firebase                  : '3.2.2',
                  ktlint                    : '0.15.0',
                  ktlintGradle              : '3.0.0',
                  glide                     : '4.5.0']
  ext.deps = [
      kotlin     : [
          stdLib: [
              jre: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
          ],
      ],
      support    : [
          design            : "com.android.support:design:$versions.supportLibrary",
          recyclerView      : "com.android.support:recyclerview-v7:$versions.supportLibrary",
          customtabs        : "com.android.support:customtabs:$versions.supportLibrary",
          cardview          : "com.android.support:cardview-v7:$versions.supportLibrary",
          'constraintLayout': "com.android.support.constraint:constraint-layout:$versions.constraintLayout",
      ],
      lifecycle  : [
          runtime   : "android.arch.lifecycle:runtime:$versions.archComponents",
          extentions: "android.arch.lifecycle:extensions:$versions.archComponents",
          compiler  : "android.arch.lifecycle:compiler:$versions.archComponents",
      ],
      rxjava2    : [
          core   : "io.reactivex.rxjava2:rxjava:2.1.8",
          android: "io.reactivex.rxjava2:rxandroid:2.0.1",
          kotlin : "io.reactivex.rxjava2:rxkotlin:2.2.0",
      ],
      glide      : [
          core    : "com.github.bumptech.glide:glide:$versions.glide",
          okhttp3 : "com.github.bumptech.glide:okhttp3-integration:$versions.glide",
          compiler: "com.github.bumptech.glide:compiler:$versions.glide",
      ],
      databinding: [
          compiler: "com.android.databinding:compiler:$versions.gradlePlugin",
      ],
      leakcanary : [
          op  : "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",
          noOp: "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary",
      ],
      firebase   : [
          uiAuth: "com.firebaseui:firebase-ui-auth:$versions.firebase",
          crash : "com.google.firebase:firebase-crash:11.8.0",
      ],
      map        : [
          google: "com.google.android.gms:play-services-maps:11.8.0",
          utils : "com.google.maps.android:android-maps-utils:0.5",
      ],
      dagger     : [
          core       : "com.google.dagger:dagger:$versions.dagger",
          'android'  : "com.google.dagger:dagger-android:$versions.dagger",
          'support'  : "com.google.dagger:dagger-android-support:$versions.dagger",
          'compiler' : "com.google.dagger:dagger-compiler:${versions.dagger}",
          'processor': "com.google.dagger:dagger-android-processor:$versions.dagger"
      ],
      ktx        : "androidx.core:core-ktx:0.2",
      'timber'   : "com.jakewharton.timber:timber:$versions.timber",
      'junit'    : "junit:junit:$versions.junit"
  ]
  repositories {
    google()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$versions.gradlePlugin"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
    classpath 'com.google.gms:google-services:3.1.1'
    classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$versions.ktlintGradle"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task upgradeWrapper(type: Wrapper) {
  description 'Creates the gradle wrapper.'
  gradleVersion '4.5'
}
