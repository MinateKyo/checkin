// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.application = [id: 'com.addhen.checkin']
  ext.versions = [kotlin                    : '1.2.50',
                  gradlePlugin              : '3.2.0-beta02',
                  minSdkVersion             : 23,
                  targetSdkVersion          : 28,
                  compileSdkVersion         : 28,
                  supportLibrary            : '1.0.0-alpha1',
                  constraintLayout          : '1.1.0',
                  leakcanary                : '1.5.4',
                  sourceCompatibilityVersion: JavaVersion.VERSION_1_8,
                  targetCompatibilityVersion: JavaVersion.VERSION_1_8,
                  dagger                    : '2.16',
                  archComponents            : '2.0.0-alpha1',
                  timber                    : '4.6.1',
                  junit                     : '4.12',
                  firebase                  : '3.2.2',
                  ktlint                    : '0.15.0',
                  ktlintGradle              : '3.0.0',
                  glide                     : '4.8.0-SNAPSHOT',
                  detekt                    : 'RC6-3']
  ext.deps = [
      kotlin     : [
          stdLib: [
              jdk: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
          ],
      ],
      support    : [
          design            : "com.google.android.material:material:$versions.supportLibrary",
          recyclerView      : "androidx.recyclerview:recyclerview:$versions.supportLibrary",
          customtabs        : "androidx.browser:browser:$versions.supportLibrary",
          cardview          : "androidx.cardview:cardview:$versions.supportLibrary",
          'constraintLayout': "androidx.constraintlayout:constraintlayout:$versions.constraintLayout",
      ],
      lifecycle  : [
          runtime   : "androidx.lifecycle:lifecycle-runtime:$versions.archComponents",
          extentions: "androidx.lifecycle:lifecycle-extensions:$versions.archComponents",
          compiler  : "androidx.lifecycle:lifecycle-compiler:$versions.archComponents",
      ],
      rxjava2    : [
          core   : "io.reactivex.rxjava2:rxjava:2.1.8",
          android: "io.reactivex.rxjava2:rxandroid:2.0.1",
          kotlin : "io.reactivex.rxjava2:rxkotlin:2.2.0",
      ],
      glide      : [
          core    : "com.github.bumptech.glide:glide:$versions.glide",
          okhttp3 : "com.github.bumptech.glide:okhttp3-integration:$versions.glide",
          compiler: "com.github.bumptech.glide:compiler:$versions.glide",
      ],
      databinding: [
          compiler: "androidx.databinding:databinding-compiler:$versions.gradlePlugin",
      ],
      leakcanary : [
          op  : "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",
          noOp: "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary",
      ],
      firebase   : [
          uiAuth: "com.firebaseui:firebase-ui-auth:$versions.firebase",
          crash : "com.google.firebase:firebase-crash:11.8.0",
      ],
      map        : [
          google: "com.google.android.gms:play-services-maps:11.8.0",
          utils : "com.google.maps.android:android-maps-utils:0.5",
      ],
      dagger     : [
          core       : "com.google.dagger:dagger:$versions.dagger",
          'android'  : "com.google.dagger:dagger-android:$versions.dagger",
          'support'  : "com.google.dagger:dagger-android-support:$versions.dagger",
          'compiler' : "com.google.dagger:dagger-compiler:${versions.dagger}",
          'processor': "com.google.dagger:dagger-android-processor:$versions.dagger"
      ],
      ktx        : "androidx.core:core-ktx:1.0.0-alpha1",
      'timber'   : "com.jakewharton.timber:timber:$versions.timber",
      'junit'    : "junit:junit:$versions.junit",
  ]
  repositories {
    google()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$versions.gradlePlugin"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    classpath 'com.google.gms:google-services:3.2.0'
    classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$versions.ktlintGradle"
    classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0.$versions.detekt"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

apply plugin: 'io.gitlab.arturbosch.detekt'

allprojects {
  repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
    // Using glide's snapshot release for a fix for Jetpack because
    // the stable version of Glide doesn't use Jetpack's namespacess
    // even though Jetifier is enabled.
    maven {
      name 'glide-snapshot'
      url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task upgradeWrapper(type: Wrapper) {
  description 'Creates the gradle wrapper.'
  gradleVersion '4.5'
}

detekt {
  version = "1.0.0.$versions.detekt"
  profile("main") {
    input = "$projectDir/app/src/main/java"
    config = "$projectDir/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
}

